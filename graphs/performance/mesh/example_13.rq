# 13: Display MeSH Qualifiers grouped 
# by the their descriptors' base tree numbers.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
PREFIX mesh2017: <http://id.nlm.nih.gov/mesh/2017/>

SELECT ?qualifier ?baseTreeNum (count(?baseTreeNum) AS ?count) WHERE {
SELECT ?qualifier (SUBSTR(?tnLabel,1,3) AS ?baseTreeNum)

WHERE {
?dq a meshv:AllowedDescriptorQualifierPair .
?dq meshv:hasDescriptor ?d .
?dq meshv:hasQualifier ?q .
?d meshv:treeNumber ?tn .
?tn rdfs:label ?tnLabel .
?q meshv:preferredTerm ?pt .
?pt meshv:prefLabel ?qualifier
FILTER(?qualifier = 'adverse effects'@en)
#full list of qualifiers to choose from:
#http://www.nlm.nih.gov/mesh/topsubscope.html
}
}
GROUP BY ?qualifier ?baseTreeNum
ORDER BY ?baseTreeNum